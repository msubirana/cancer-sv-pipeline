from os.path import basename
from glob import glob
from types import SimpleNamespace
import os
import utils

configfile: 'conf/config.yaml'

# TODO: how to wait for all samples excord in giggle?

# EXAMPLE: config['ABC'] becomes config.ABC
# This allows for dot (.) access instead of dict access
# of configuration parameters.
config = SimpleNamespace(**config)

# file follows {sample}_{BL|TI}.bam name scheme
samples = list(set([basename(x).split('_')[0]
    for x in glob(os.path.join(config.bam_dir, '*.bam'))]))

rule All:
    input:
        f'{config.install_dir}/manta-{config.manta_version}.centos6_x86_64/bin/configManta.py',
        f'{config.install_dir}/excord',
        f'{config.install_dir}/duphold',
        f'{config.install_dir}/giggle/bin/giggle',
        f'{config.install_dir}/stix/bin/stix',
        expand(f'{config.outdir}/manta/{{sample}}_pass.vcf', sample=samples),
        expand(f'{config.outdir}/delly/{{sample}}_pass.vcf', sample=samples),
        expand(f'{config.outdir}/gridss/{{sample}}_pass.vcf', sample=samples), 
        expand(f'{config.outdir}/smoove/{{sample}}_pass.vcf', sample=samples), 

  
# ==============================================================================
# Installation Rules
# ==============================================================================
rule InstallExcord:
    """
    Pull excord bin from github release
    """
    input:
        script = "scripts/install_excord.sh",
    output:
        binary_excord = f'{config.install_dir}/excord',
    shell:
        """
        {input.script} {config.install_dir} {config.excord_version}
        """
rule InstallGiggle:
    """
    Build giggle from source
    """
    input:
        script = "scripts/install_giggle.sh",
    output:
        giggle = f'{config.install_dir}/giggle/bin/giggle', 
    shell:
        """
        {input.script} {config.install_dir}
        """
rule InstallStix:
    """
    Build stix and dependencies from source (Depends on giggle)
    """
    input:
        script = "scripts/install_stix.sh",
        giggle_path = f'{rules.InstallGiggle.output.giggle}', # wait for giggle
    output:
        stix = f'{config.install_dir}/stix/bin/stix',
    shell:
        """
        {input.script} {config.install_dir} {input.giggle_path}
        """
rule InstallManta:
    """
    Install manta from github release.
    """
    input:
        script = "scripts/install_manta.sh",
    output:
        # config is in bin/configManta.py
        installation = f'{config.install_dir}/manta-{config.manta_version}.centos6_x86_64/bin/configManta.py',
    shell:
        """
        {input.script} {config.install_dir} {config.manta_version}
        """
rule InstallDuphold:
    """
    Pull Duphold bin from github release
    """
    input:
        script = "scripts/install_duphold.sh",
    output:
        binary_duphold = f'{config.install_dir}/duphold',
    shell:
        """
        {input.script} {config.install_dir} {config.duphold_version}
        """
# ==============================================================================
# Create a STIX index with tumor samples
# ==============================================================================
rule ExcordTumors:
    """
    Extract discordant/split reads from tumor BAMs
    """
    input:
        script = "scripts/Excord.sh",
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
        reference = f'{config.reference}',
        excord = f'{rules.InstallExcord.output.binary_excord}',
    output:
        f'{config.outdir}/excord/tumors/{{sample}}.bed.gz',
    log:
        f'{config.outdir}/logs/ExcordTumors_{{sample}}.log',
    conda:
        'envs/excord.yaml',
    shell:
        """
        {input.script} {input.tumor_bam} {input.reference} {output.bed} {input.excord}
        """
rule ExcordCtrls:
    """
    Extract discordant/split reads from tumor BAMs
    """
    input:
        script = "scripts/Excord.sh",
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
        reference = f'{config.reference}',
        excord = f'{rules.InstallExcord.output.binary_excord}',
    output:
        f'{config.outdir}/excord/ctrls/{{sample}}.bed.gz',
    log:
        f'{config.outdir}/logs/ExcordCtrls_{{sample}}.log',
    conda:
        'envs/excord.yaml',
    shell:
        """
        {input.script} {input.tumor_bam} {input.reference} {output.bed} {input.excord}
        """
    
rule MakeGiggleIndexTumors:
    """
    Create giggle index from excord bedpe's
    """
    input:
        script = "scripts/MakeGiggleIndex.sh",
        giggle = f'{rules.InstallGiggle.output.giggle}',
        excord_path = f'{config.outdir}/excord',
    output:
        directory(f'{config.outdir}/giggle/tumor'),
    log:
        f'{config.outdir}/logs/MakeGiggleIndexTumors.log',
    shell:
        """
        {input.script} {input.giggle} {input.excord_path} {output}
        """

rule MakeGiggleIndexCtrls:
    """
    Create giggle index from excord bedpe's
    """
    input:
        script = "scripts/MakeGiggleIndex.sh",
        giggle = f'{rules.InstallGiggle.output.giggle}',
        excord_path = f'{config.outdir}/excord',
    output:
        directory(f'{config.outdir}/giggle/ctrl'),
    log:
        f'{config.outdir}/logs/MakeGiggleIndexCtrls.log',
    shell:
        """
        {input.script} {input.giggle} {input.excord_path} {output}
        """
    
rule MakeSTIXIndexTumors:
    """
    Create STIX index from giggle index
    """
    input:
        script = "scripts/MakeSTIXIndex.sh",
        stix = f'{rules.InstallStix.output.stix}',
        giggle_index = f'{rules.MakeGiggleIndexTumors.output}',
        ped = '/home/msubirana/Documents/phd/repos/cancer-sv-pipeline/results/giggle/tumors.ped'
    output:
        f'{config.outdir}/stix/tumor.ped.db',
    log:
        f'{config.outdir}/logs/MakeSTIXIndexTumors_.log',
    shell:
        """
        {input.script} {input.stix} {input.giggle_index} {output} {input.ped}
        """

rule MakeSTIXIndexCtrls:
    """
    Create STIX index from giggle index
    """
    input:
        script = "scripts/MakeSTIXIndex.sh",
        stix = f'{rules.InstallStix.output.stix}',
        giggle_index = f'{rules.MakeGiggleIndexCtrls.output}',
        ped = '/home/msubirana/Documents/phd/repos/cancer-sv-pipeline/results/giggle/ctrls.ped'
    output:
        f'{config.outdir}/stix/ctrl.ped.db',
    log:
        f'{config.outdir}/logs/MakeSTIXIndexCtrls.log',
    shell:
        """
        {input.script} {input.stix} {input.giggle_index} {output} {input.ped}
        """
# ==============================================================================
# GRIDSS Rules
# ==============================================================================
rule gridss:  
    input:
        script = 'scripts/gridss.sh',
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
        normal_bam = f'{config.bam_dir}/{{sample}}_BL.bam',
        reference = f'{config.reference}',
        exclude = f'{config.exclude_bed}'
    output:
        f'{config.outdir}/gridss/{{sample}}.vcf',
    threads:
        workflow.cores
    log:
        f'{config.outdir}/logs/gridss_{{sample}}.log',
    conda:
        'envs/gridss.yaml',
    shell:
        """
        {input.script} {input.reference} {output} {input.exclude} {input.normal_bam} {input.tumor_bam} {threads}
        """

rule annotatedGridss:
    """
    Filter PASS variants Gridss
    """
    input:
        script = 'scripts/sv_type_infer_gridss.R',
        input_vcf = f'{rules.gridss.output}',
    output:
        f'{config.outdir}/gridss/{{sample}}_ann.vcf',
    log:
        f'{config.outdir}/logs/annotatedGridss{{sample}}.log',    
    conda:
        'envs/gridss.yaml',
    shell:
        """
        Rscript {input.script} {input.input_vcf} {output}
        """

rule filterDupholdGridss:
    """
    add depth information to those calls and filter false SVs
    """
    input:
        duphold = f'{rules.InstallDuphold.output.binary_duphold}',
        script = 'scripts/duphold.sh',
        input_vcf = f'{rules.annotatedGridss.output}',
        reference = f'{config.reference}',
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
    output:
        f'{config.outdir}/gridss/{{sample}}_dup.vcf',
    threads:
        workflow.cores
    log:
        f'{config.outdir}/logs/filterDupholdGridss{{sample}}.log',    
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.duphold} {input.input_vcf} {input.tumor_bam} {input.reference} {output} {threads}
        """
rule filterGridss:
    """
    Filter PASS variants Gridss
    """
    input:
        script = 'scripts/bcftools_filter.sh',
        input_vcf = f'{rules.filterDupholdGridss.output}',
    output:
        f'{config.outdir}/gridss/{{sample}}_pass.vcf',
    log:
        f'{config.outdir}/logs/filterGridss_{{sample}}.log',    
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.input_vcf} {output}
        """



# ==============================================================================
# Delly Rules
# ==============================================================================
rule delly_bcf:
    input:
        ref = f'{config.reference}',
        alns = [f'{config.bam_dir}/{{sample}}_TI.bam', f'{config.bam_dir}/{{sample}}_BL.bam'],
        exclude = f'{config.exclude_bed}',
    output:
        f'{config.outdir}/delly/{{sample}}.bcf',
    conda:
        'envs/delly.yaml',
    params:
        uncompressed_bcf=True,
    log:
        f'{config.outdir}/logs/delly_bcf_{{sample}}.log',   
    threads: 2  # It is best to use as many threads as samples
    wrapper:
        "v1.19.2/bio/delly"

rule bcfToVcfDelly:
    input:
        vcf = f'{rules.delly_bcf.output}',
        script = 'scripts/bcf_to_vcf.sh',
    output:
        f'{config.outdir}/delly/{{sample}}.vcf',
    log:
        f'{config.outdir}/logs/bcfToVcfDelly_{{sample}}.log',    
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.vcf} {output}
        """

rule filterDupholdDelly:
    """
    add depth information to those calls and filter false SVs
    """
    input:
        duphold = f'{rules.InstallDuphold.output.binary_duphold}',
        script = 'scripts/duphold.sh',
        input_vcf = f'{rules.bcfToVcfDelly.output}',
        reference = f'{config.reference}',
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
    output:
        f'{config.outdir}/delly/{{sample}}_dup.vcf',
    threads:
        workflow.cores
    log:
        f'{config.outdir}/logs/filterDupholdDelly{{sample}}.log',    
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.duphold} {input.input_vcf} {input.tumor_bam} {input.reference} {output} {threads}
        """

rule filterDelly:
    """
    Filter PASS variants Delly
    """
    input:
        script = 'scripts/bcftools_filter.sh',
        input_vcf = f'{rules.filterDupholdDelly.output}',
    output:
        f'{config.outdir}/delly/{{sample}}_pass.vcf',
    log:
        f'{config.outdir}/logs/filterDelly_{{sample}}.log',   
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.input_vcf} {output}
        """

# ==============================================================================
# MANTA Rules
# ==============================================================================

rule ConfigManta:
    """
    Configure the manta calling workflow
    """
    input:
        script = 'scripts/conf_manta.sh',
        configManta = f'{rules.InstallManta.output.installation}',
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
        normal_bam = f'{config.bam_dir}/{{sample}}_BL.bam',
        reference = f'{config.reference}',
    params:
        rundir = f'{config.outdir}/manta/{{sample}}_manta',
    output:
        runPy = f'{config.outdir}/manta/{{sample}}_manta/runWorkflow.py',
    log:
        f'{config.outdir}/logs/ConfigManta_{{sample}}.log',
    shell:
        """
        {input.script} {input.configManta} \
                    {input.tumor_bam} \
                    {input.normal_bam} \
                    {input.reference} \
                    {params.rundir} 
        """
        
rule RunManta:
    """
    Run manta
    """
    input:
        script = 'scripts/run_manta.sh',
        rundir = f'{rules.ConfigManta.output.runPy}',
    output: 
        f'{config.outdir}/manta/{{sample}}_manta/results/variants/somaticSV.vcf.gz',
    threads:
        workflow.cores
    log:
        f'{config.outdir}/logs/RunManta_{{sample}}.log',
    shell:
        """
        {input.script} {input.rundir} {threads}
        """

rule filterDupholdManta:
    """
    add depth information to those calls and filter false SVs
    """
    input:
        duphold = f'{rules.InstallDuphold.output.binary_duphold}',
        script = 'scripts/duphold.sh',
        input_vcf = f'{rules.RunManta.output}',
        reference = f'{config.reference}',
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
    output:
        f'{config.outdir}/manta/{{sample}}_dup.vcf',
    threads:
        workflow.cores
    log:
        f'{config.outdir}/logs/filterDupholdManta{{sample}}.log',    
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.duphold} {input.input_vcf} {input.tumor_bam} {input.reference} {output} {threads}
        """

rule filterManta:
    """
    Filter PASS variants manta
    """
    input:
        script = 'scripts/bcftools_filter.sh',
        input_vcf = f'{rules.filterDupholdManta.output}',
    output:
        f'{config.outdir}/manta/{{sample}}_pass.vcf',
    log:
        f'{config.outdir}/logs/filterManta_{{sample}}.log',
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.input_vcf} {output}
        """

# ==============================================================================
# Lumpy will just report all SVs so filtering will need to be done
# with STIX to get rid of germline variants
# ==============================================================================

rule smoove_call:
    input:
        script = 'scripts/smoove_call.sh',
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
        exclude = f'{config.exclude_bed}', 
        reference = f'{config.reference}',
    output:
        f'{config.outdir}/smoove/called/{{sample}}-smoove.genotyped.vcf.gz',
    params:
        output_dir = f'{config.outdir}/smoove/called',
    log:
        f'{config.outdir}/logs/smoove_call_{{sample}}.log',
    conda:
        'envs/smoove.yaml',
    shell:
        """
        {input.script} {params.output_dir} {input.exclude} {wildcards.sample} {input.reference} {input.tumor_bam}
        """
rule smoove_genotype:
    input: 
        script = 'scripts/smoove_genotype.sh',
        vcf = rules.smoove_call.output,
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
        reference = f'{config.reference}',
    output:
        f'{config.outdir}/smoove/genotyped/{{sample}}-smoove.genotyped.vcf.gz',
    log:
        f'{config.outdir}/logs/smoove_genotype_{{sample}}.log',
    params:
        output_dir = f'{config.outdir}/smoove/genotyped',
    conda:
        'envs/smoove.yaml',
    shell: 
        """
        {input.script} {wildcards.sample} {params.output_dir} {input.reference} {input.vcf} {input.tumor_bam}
        """

rule bcfToVcfSmoove:
    input:
        vcf = f'{rules.smoove_genotype.output}',
        script = 'scripts/bcf_to_vcf.sh',
    output:
        f'{config.outdir}/smoove/{{sample}}.vcf',
    log:
        f'{config.outdir}/logs/bcfToVcfSmoove_{{sample}}.log',    
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.vcf} {output}
        """

rule filterDupholdSmoove:
    """
    add depth information to those calls and filter false SVs
    """
    input:
        duphold = f'{rules.InstallDuphold.output.binary_duphold}',
        script = 'scripts/duphold.sh',
        input_vcf = f'{rules.bcfToVcfSmoove.output}',
        reference = f'{config.reference}',
        tumor_bam = f'{config.bam_dir}/{{sample}}_TI.bam',
    output:
        f'{config.outdir}/smoove/{{sample}}_dup.vcf',
    threads:
        workflow.cores
    log:
        f'{config.outdir}/logs/filterDupholdSmoove{{sample}}.log',    
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.duphold} {input.input_vcf} {input.tumor_bam} {input.reference} {output} {threads}
        """

rule filterSmoove:
    """
    Filter PASS variants smoove
    """
    input:
        script = 'scripts/bcftools_filter.sh',
        input_vcf = f'{rules.filterDupholdSmoove.output}',
    output:
        f'{config.outdir}/smoove/{{sample}}_pass.vcf',
    log:
        f'{config.outdir}/logs/filterSmoove_{{sample}}.log',
    conda:
        'envs/post_processing.yaml',
    shell:
        """
        {input.script} {input.input_vcf} {output}
        """
# ==============================================================================
# TODO STIX filtering for germline SV callers
# ==============================================================================
